 R←Test_MailParms_05(stopFlag batchFlag);⎕IO;⎕ML;⎕TRAP;myParams;dmx;buff
⍝ Check the "Body" property
 ⎕IO←1 ⋄ ⎕ML←1
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 myParams←⎕NEW SMTP.MailParameters

 myParams.Body←buff←'This and that'
 →T.PassesIf(,⊂buff)≡myParams.Body
 myParams.Body←buff←'This and that' 'and much more'
 →T.PassesIf buff≡myParams.Body

 :Trap 0
     myParams.Body←1 2
     →T.PassesIf 0
 :Else
     dmx←⎕DMX
     →T.PassesIf'Body: neither a simple character vector nor a vector of character vectors'≡dmx.EM
 :EndTrap

 :Trap 0
     myParams.Body←⊂⊂⊂'Text'
     →T.PassesIf 0
 :Else
     dmx←⎕DMX
     →T.PassesIf'Body: neither a simple character vector nor a vector of character vectors'≡dmx.EM
 :EndTrap

 R←T._OK
